dnl Process this file with autoconf to produce a configure script.

AC_INIT(freedesktop.org.xml.in)
AM_INIT_AUTOMAKE(shared-mime-info, 0.13.1)

AC_PROG_INTLTOOL

AM_CONFIG_HEADER(config.h)

GETTEXT_PACKAGE=shared-mime-info
AC_SUBST(GETTEXT_PACKAGE)
ALL_LINGUAS="az cy fi fr hu nl nn no sv sr"
AM_GLIB_GNU_GETTEXT

dnl Check pkg-config is installed
if test -z "$PKG_CONFIG"; then
  PKG_CONFIG=pkg-config
fi
AC_SUBST(PKG_CONFIG)

AC_MSG_CHECKING(that pkg-config runs)
if "$PKG_CONFIG" --version > /dev/null 2>&1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([*** $PKG_CONFIG missing ***

\'$PKG_CONFIG --version\' failed to run - make sure it is inside one of the
directories in your PATH environment variable!

pkg-config is available from this web page:

http://www.freedesktop.org/software/pkgconfig
])
fi

AC_DEFUN(REQUIRE, [
AC_MSG_CHECKING(that $1 (version >= $2) is installed)
if "$PKG_CONFIG" --atleast-version=$2 $1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  if "$PKG_CONFIG" --exists $1 ; then
    AC_MSG_ERROR(Current version is only `"$PKG_CONFIG" "$1" --modversion`)
  else
    AC_MSG_ERROR(Package is not installed)
  fi
fi
])

dnl Check for libxml and glib
REQUIRE(libxml-2.0, 2.4.0)
REQUIRE(glib-2.0, 2.0.0)

dnl Checks for programs.
AC_PROG_CC

dnl Use -Wall, etc if possible
[
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall -Wstrict-prototypes -Wmissing-prototypes -Wwrite-strings" ;;
  esac
fi
]
	
AC_ARG_ENABLE(update-mimedb,
   AC_HELP_STRING([--disable-update-mimedb],
                   [disable the update-mime-database after install [default=no]]),,
    enable_update_mimedb=yes)
 AM_CONDITIONAL(ENABLE_UPDATE_MIMEDB, test x$enable_update_mimedb = xyes)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

mimedir=['${datadir}/mime']
AC_SUBST(mimedir)

AC_OUTPUT(
Makefile
shared-mime-info.pc
po/Makefile.in
)
